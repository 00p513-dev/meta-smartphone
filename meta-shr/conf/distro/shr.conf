#-----------------------------------------------------------------------------
#@TYPE: Distribution
#@NAME: SHR
#@DESCRIPTION: Linux Distribution Configuration for Mobile Phones
#-----------------------------------------------------------------------------

BB_LOCALCOUNT_OVERRIDE = ""
require conf/distro/include/preferred-shr-versions.inc
require conf/distro/include/efl-from-svn.inc

#
# Header
#
DISTRO_NAME = "shr"
DISTRO_VERSION = "${DATE}"
DISTRO_REVISION = "2"
DISTRO_PR = ".6"
DISTRO_TYPE = "release"
DISTRO = "shr"

VIRTUAL-RUNTIME_apm = "fso-apm"
VIRTUAL-RUNTIME_login_manager = "shadow util-linux-agetty"
VIRTUAL-RUNTIME_dev_manager = ""
VIRTUAL-RUNTIME_init_manager = "sysvinit sysvinit-pidof"
VIRTUAL-RUNTIME_initscripts = "initscripts"
VIRTUAL-RUNTIME_graphical_init_manager = "xserver-nodm-init"
VIRTUAL-RUNTIME_xserver_common = "xserver-common"

# we need this for systemd
KERNEL_ENABLE_CGROUPS ?= "1"

#Generate locales on the buildsystem instead of on the target. Speeds up first boot, set to "1" to enable
ENABLE_BINARY_LOCALE_GENERATION ?= "1"

# We only want to build UTF8 locales
LOCALE_UTF8_ONLY = "1"

# Yocto work around for missing ONLINE_PACKAGE_MANAGEMENT support, needed for testlab
ONLINE_PACKAGE_MANAGEMENT = "full"

#save statistical info on images
INHERIT += "testlab"
INHERIT += "packagehistory"

#activate the blacklister
INHERIT += "blacklist"

# libfoo -> libfoo0-2 (etc)
INHERIT += "debian"

QA_LOG = "1"

ANGSTROM_BLACKLIST_pn-bluez-libs  = "bluez-libs 3.x has been replaced by bluez4"
ANGSTROM_BLACKLIST_pn-bluez-utils = "bluez-utils 3.x has been replaced by bluez4"
ANGSTROM_BLACKLIST_pn-atd = "atd has been replaced by atd-over-fso"
ANGSTROM_BLACKLIST_pn-qt4-x11-free-gles = "qt4-x11-free is fine even without gles"
ANGSTROM_BLACKLIST_pn-xserver-xf86-lite = "xorg-xserver is preferred"
ANGSTROM_BLACKLIST_pn-libiconv = "libiconv is provided by LIBC"
ANGSTROM_BLACKLIST_pn-libiconv-native = "libiconv-native is provided by host LIBC (no provider in OE)"
ANGSTROM_BLACKLIST_pn-gconf-dbus = "gconf-dbus is provided by gconf"
ANGSTROM_BLACKLIST_pn-gdbus-binding-tool = "gdbus-binding-tool is provided by gdbus-codegen from glib itself"
ANGSTROM_BLACKLIST_pn-gdbus-binding-tool-native = "gdbus-binding-tool is provided by gdbus-codegen from glib-native itself"
ANGSTROM_BLACKLIST_pn-external-csl-toolchain = "use internal toolchain"
ANGSTROM_BLACKLIST_pn-external-poky-toolchain = "use internal toolchain"

#The shr-mirrors.bbclass repleaces this
#PREMIRRORS = "(ftp|https?)$://.*/.* http://build.shr-project.org/sources/"
INHERIT += "shr-mirrors" 

# our subversion-native is new enough for this
FETCHCMD_svn = "/usr/bin/env svn --non-interactive --trust-server-cert"

# bump PRINC in meta-shr/recipes-core/meta/distro-feed-configs.bbappend after changing this
DISTRO_FEED_PREFIX ?= "shr-2012.01"
DISTRO_FEED_URI ?= "http://build.shr-project.org/shr-2012.01/ipk"
DEPLOY_DIR_IMAGE = "${DEPLOY_DIR}/images/${MACHINE}"

ESYSACTIONS = "e-wm-sysactions-shr"
EMENU = "e-wm-menu-shr"
ECONFIG = "e-wm-config-illume2-shr"
ETHEME = "e-wm-theme-illume-gry"

# From minimal.conf

#############################################################################
# FEATURE SELECTION
#############################################################################
# We want images supporting the following features (for task-base)
DISTRO_FEATURES = "largefile ipv4 ipv6 wifi ppp ext2 vfat bluetooth usbgadget usbhost xattr"
# Following features are for ARM and E500 based machines
DISTRO_FEATURES += " eabi "
# Use-like features
DISTRO_FEATURES += " tk "
DISTRO_FEATURES += " x11 "
DISTRO_FEATURES += " pam "

# Inherit the default LIBC features superset from OE-core
DISTRO_FEATURES += "${DISTRO_FEATURES_LIBC}"

#############################################################################
# Ensure MACHINE_CLASS is in OVERRIDES and is not empty as :: in OVERRIDES breaks order of OVERRIDES ie in FILESPATH
#############################################################################
MACHINE_CLASS ?= "${MACHINE}"
MACHINEOVERRIDES .= ":${MACHINE_CLASS}"
MACHINEOVERRIDES[vardepsexclude] = "MACHINE"
OVERRIDES[vardepsexclude] = "MACHINE"

#############################################################################
# Enable thumb for architectures where it makes sense
#############################################################################
# We can't set ARM_INSTRUCTION_SET_<override> directly since that will un-overridable in recipes like gcc
PREFERRED_ARM_INSTRUCTION_SET              ?= "thumb"
ARM_INSTRUCTION_SET = "${PREFERRED_ARM_INSTRUCTION_SET}"

#############################################################################
# PREFERRED PROVIDERS
#############################################################################
PREFERRED_PROVIDER_libcanberra-alsa = "libcanberra"
PREFERRED_PROVIDER_gpsd = "fso-gpsd"
PREFERRED_PROVIDER_atd = "atd-over-fso"
PREFERRED_PROVIDER_glib-2.0-utils = "glib-2.0"
PREFERRED_PROVIDER_gconf-native = "gconf-native"
PREFERRED_PROVIDER_gconf-dbus = "gconf"
PREFERRED_PROVIDER_libfribidi = "libfribidi"
PREFERRED_PROVIDER_gconf = "gconf"
PREFERRED_PROVIDER_virtual/libx11 = "libx11"
PREFERRED_PROVIDER_virtual/xserver-xf86 = "xserver-xorg"
PREFERRED_PROVIDER_abiword = "abiword"
PREFERRED_PROVIDER_mplayer = "mplayer2"
# Prefer libjpeg-turbo
PREFERRED_PROVIDER_jpeg = "libjpeg-turbo"
PREFERRED_PROVIDER_jpeg-native = "libjpeg-turbo-native"

# virtual/libgl is used ie in xorg-xserver which is not MACHINE_ARCH so we need to keep it consistent across multiple machines using same xorg-xserver
PREFERRED_PROVIDER_virtual/libgl = "mesa-dri"

# pango depends on virtual/libiconv and BBCLASSEXTEND=native makes virtual/libiconv-native from it and we don't want libiconv-native
ASSUME_PROVIDED += "virtual/libiconv-native"

#############################################################################
# CONTENTS
#############################################################################

# pulls udev to image
BAD_RECOMMENDATIONS += "gpsd-udev"

DISTRO_EXTRA_APPS ?= ""

# Additional content I (only valid if you include task-base)
# distro-feed-configs: configuration files for the online feeds
# util-linux-mount util-linux-umount: busybox mount is broken
DISTRO_EXTRA_RDEPENDS += "\
  shr-version \
  distro-feed-configs \
  ${DISTRO_EXTRA_APPS} \
"
